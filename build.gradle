plugins {
    id 'java'
    id 'java-library'
    id 'com.gradle.build-scan' version '2.3'
    id 'org.springframework.boot' version '2.1.6.RELEASE'
}

apply plugin: 'io.spring.dependency-management'

group 'com.github.coleb1911'
version '1.0'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    // Spring and Hibernate
    // Logback is excluded in order to avoid conflict with TinyLog
    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.1.6.RELEASE') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    implementation group: 'org.hibernate', name: 'hibernate-core', version: '5.4.3.Final'

    // Spring Web for consuming REST APIs
    // Logging framework is, again, excluded to avoid conflict
    implementation (group: 'org.springframework.data', name: 'spring-data-rest-webmvc', version: '2.1.6.RELEASE') {
        exclude group: 'org.slf4j', module: 'jcl-over-slf4j'
    }

    // Other libraries & APIs
    implementation group: 'com.discord4j', name: 'discord4j-core', version: '3.0.7'
    implementation group: 'org.aeonbits.owner', name: 'owner', version: '1.0.10'
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.11'
    implementation group: 'org.tinylog', name: 'tinylog', version: '1.3.6'
    implementation group: 'org.tinylog', name: 'slf4j-binding', version: '1.3.6'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.3.1'
    implementation group: 'com.h2database', name: 'h2', version: '1.4.199'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    // JUnit
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.0'

    // Apache utilities for build tools
    // Excluded in final build
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.9'
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.18'
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
}

test {
    useJUnitPlatform()
    systemProperty 'ghost2.version', project.version
}

bootJar {
    exclude 'ghost.properties'
    exclude '**/buildtools'
    exclude '**/httpclient-*.jar'
    exclude '**/httpcore-*.jar'
    exclude '**/commons-compress-*.jar'
    exclude '**/commons-io-*.jar'
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
}

compileJava {
    inputs.property("moduleName", "ghost2.main")
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

javadoc {
    inputs.property("moduleName", "ghost2.main")
    source = sourceSets.main.allJava
    doFirst {
        options.addStringOption('-module-path', classpath.asPath)
        options.addBooleanOption('html5', true)
    }
}

buildScan {
	termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

task ghostPackage(type:JavaExec) {
    dependsOn bootJar
    main = 'com.github.coleb1911.ghost2.buildtools.RuntimeBuilder'
    classpath = sourceSets.main.runtimeClasspath
    group = 'build'
}

clean {
    delete "${rootDir}/bt_temp"
    delete "${rootDir}/bt_out"
    delete "${rootDir}/bt_xplatform"
}